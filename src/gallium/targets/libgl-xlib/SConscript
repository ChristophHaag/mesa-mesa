#######################################################################
# SConscript for xlib winsys

Import('*')

env = env.Clone()

env.Append(CPPPATH = [
    '#/src/mapi',
    '#/src/mesa',
    '#/src/mesa/main',
    '#src/gallium/state_trackers/glx/xlib',
])

env.Append(CPPDEFINES = ['USE_XSHM'])

env.Prepend(LIBS = env['X11_LIBS'])

env.Prepend(LIBS = [
    st_xlib,
    ws_xlib,
    trace,
    rbug,
    identity,
    glapi,
    mesa,
    glsl,
    gallium,
    'talloc'
])

sources = [
    'xlib.c',
]

if True:
    env.Append(CPPDEFINES = 'GALLIUM_SOFTPIPE')
    env.Prepend(LIBS = [softpipe])

if True:
    env.Append(CPPDEFINES = 'GALLIUM_GALAHAD')
    env.Prepend(LIBS = [galahad])

if env['llvm']:
    env.Append(CPPDEFINES = 'GALLIUM_LLVMPIPE')
    env.Prepend(LIBS = [llvmpipe])
    
if False:
    # TODO: Detect Cell SDK
    env.Append(CPPDEFINES = 'GALLIUM_CELL')
    env.Prepend(LIBS = [cell])

# TODO: write a wrapper function http://www.scons.org/wiki/WrapperFunctions
libgl = env.SharedLibrary(
    target ='GL',
    source = sources,
)

if True:
    # XXX: Only install this libGL.so if DRI not enabled
    libgl = env.InstallSharedLibrary(libgl, version=(1, 5))

env.Alias('libgl-xlib', libgl)
