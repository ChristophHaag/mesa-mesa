#######################################################################
# SConscript for xlib winsys

Import('*')

if env['platform'] != 'linux':
    Return()

if not set(('softpipe', 'llvmpipe', 'cell')).intersection(env['drivers']):
    print 'warning: no supported pipe driver: skipping build of xlib libGL.so'
    Return()

env = env.Clone()

env.Prepend(LIBS = [
    ws_xlib,
    trace,
    identity,
#    gallium,
])

env.Append(CPPPATH = [
    '#src/gallium/drivers',
])


sources = [
    'graw_xlib.c',
]

if 'softpipe' in env['drivers']:
    env.Append(CPPDEFINES = 'GALLIUM_SOFTPIPE')
    env.Prepend(LIBS = [softpipe])

if 'llvmpipe' in env['drivers']:
    env.Tool('llvm')
    if 'LLVM_VERSION' in env:
        env.Append(CPPDEFINES = 'GALLIUM_LLVMPIPE')
        env.Tool('udis86')
        env.Prepend(LIBS = [llvmpipe])

# Need this for trace, identity drivers referenced by
# gallium_wrap_screen().
#
env.Prepend(LIBS = [gallium])

# TODO: write a wrapper function http://www.scons.org/wiki/WrapperFunctions
graw = env.SharedLibrary(
    target ='graw',
    source = sources,
)

env.InstallSharedLibrary(graw, version=(1, 0))

graw = env.FindIxes(graw, 'SHLIBPREFIX', 'SHLIBSUFFIX')

Export('graw')
